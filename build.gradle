buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
}

def runeLiteVersion = '1.12.13' // latest.release
def krakenApiVersion = '2.1.26'

allprojects {
    apply plugin: 'java'

    ext.runeLiteVersion = runeLiteVersion
    ext.krakenApiVersion = krakenApiVersion

    repositories {
        maven {
            url = 'https://repo.runelite.net'
            content {
                excludeGroupByRegex "com\\.github.cbartram.*"
            }
        }
        mavenCentral()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cbartram/kraken-api")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }

        maven {
            url 'https://jitpack.io'
            content {
                includeGroup "com.github.cbartram"
            }
        }
        flatDir {
            dirs 'libs'
        }
        mavenLocal()
    }

    group = 'com.krakenplugins'

    // Create a directory for collecting all JARs
    ext.jarsDir = layout.buildDirectory.dir('plugins')
}


dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation group: 'com.github.cbartram', name:'kraken-api', version: krakenApiVersion
}

subprojects {
    dependencies {
        compileOnly group: 'com.github.cbartram', name:'kraken-api', version: krakenApiVersion
        compileOnly group: 'net.runelite', name:'client', version: runeLiteVersion
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'

        testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
        testImplementation group: 'com.github.cbartram', name:'kraken-api', version: krakenApiVersion
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release.set(11)
    }

    tasks.register('simpleJar', Jar) {
        dependsOn jar
        manifest {
            attributes(
                    'Main-Class': "com.krakenplugins.${project.name.capitalize()}Plugin",
                    'Multi-Release': true
            )
        }

        from sourceSets.main.output
        archiveClassifier.set('simple')
        archiveFileName.set("${project.name}-${project.version}.jar")
    }

    tasks.register('copySimpleJar', Copy) {
        from simpleJar
        into rootProject.jarsDir
        dependsOn simpleJar, jar
    }
}

tasks.register('buildAndCollectSimpleJars') {
    dependsOn subprojects.copySimpleJar
    description = 'Builds simple JARs for all sub-projects and copies them to build/jars'

    doLast {
        println "All simple JARs have been built and copied to ${jarsDir.get()}"
    }
}